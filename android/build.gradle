/*
 * Copyright (c) 2015. Make It Real S.L.
 */

def versionMajor = 1

def versionMinor = 5

def versionPatch = 0

def code = versionMajor * 10000 + versionMinor * 100 + versionPatch

version "${versionMajor}.${versionMinor}.${versionPatch}"

def readPassword = { ->
    def runTasks = gradle.startParameter.taskNames
    def env = System.getenv();
    if ('assembleRelease' in runTasks) {
        if (env['RELEASE_KEYSTORE_PW']) {
            return env['RELEASE_KEYSTORE_PW']
        } else {
            return System.console().readLine("\n\$ Enter keystore password: ")
        }
    } else {
        return ""
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion '21.1.2'

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionName "$version"
        versionCode code
    }

    signingConfigs {

        release {
            storeFile file("../gamecover.keystore")
            keyAlias "gc"
            storePassword readPassword()
            keyPassword storePassword
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
final SUPPORTLIB_VERSION = '23.1.1'
dependencies {
    compile "com.android.support:appcompat-v7:${SUPPORTLIB_VERSION}"
    compile "com.android.support:support-v4:${SUPPORTLIB_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORTLIB_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORTLIB_VERSION}"
    compile "com.android.support:design:${SUPPORTLIB_VERSION}"

    compile 'com.google.code.gson:gson:2.3'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.guava:guava:18.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
buildscript {
    ext.kotlin_version = '1.0.0-beta-1103'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}
